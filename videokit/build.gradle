apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}
group 'com.infullmobile.android'
version '1.1.2'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId "videokit-release"
            version project.version

            artifact sourceJar
            artifact("$buildDir/outputs/aar/videokit-release.aar")

            pom.withXml {
                asNode().appendNode('description', 'Library for video processing based on FFmpeg.')
            }
        }
    }
    repositories {
        maven {
            name "iFMPrivateRelease"
            url "s3://maven-ifm-internal/private"
            credentials(AwsCredentials) {
                accessKey project.getMavenAccessKey()
                secretKey project.getMavenSecretKey()
            }
        }
        maven {
            name "iFMPublicRelease"
            url "s3://maven-ifm-internal/public"
            credentials(AwsCredentials) {
                accessKey project.getMavenAccessKey()
                secretKey project.getMavenSecretKey()
            }
        }
    }
}

ext {
    getMavenSecretKey = {
        return project.hasProperty('mavenSecretKey') ? project['mavenSecretKey'] : ""
    }
    getMavenAccessKey = {
        return project.hasProperty('mavenAccessKey') ? project['mavenAccessKey'] : ""
    }
}

task("publishIFMPrivate", dependsOn: "publishMavenPublicationToIFMPrivateReleaseRepository") {
    group "publishing"
    description "Uploads artifacts to inFullMobile private maven repository"
}

task("publishIFMPublic", dependsOn: "publishMavenPublicationToIFMPublicReleaseRepository") {
    group "publishing"
    description "Uploads artifacts to inFullMobile public maven repository"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 25
        versionCode 3
        versionName "1.1.2"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

